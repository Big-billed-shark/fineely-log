plugins {
    id 'org.springframework.boot' version "3.0.0"
    id 'io.spring.dependency-management' version "1.1.0"
    id 'java'
}

apply plugin: "maven-publish"
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}
task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar, sourcesJar
}

group 'com.fineely'
version '1.0.3'
def artifactId = "fineely-log"
archivesBaseName = artifactId

repositories {
    maven { url 'https://maven.aliyun.com/repository/public' }
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    compileOnly "org.springframework.boot:spring-boot-starter-web"
    compileOnly 'org.springframework.kafka:spring-kafka:2.6.3'
    compileOnly 'org.springframework.cloud:spring-cloud-starter-openfeign:3.1.5'
    compileOnly 'org.apache.dubbo:dubbo-rpc-triple:3.3.0'
    compileOnly 'org.apache.dubbo:dubbo-registry-zookeeper:3.3.0'

    implementation 'org.springframework.boot:spring-boot-starter-aop:3.0.0'
    implementation 'com.alibaba:fastjson:2.0.32'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation project(":fineely-log-common")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            version project.version
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                // 构件名称
                // 区别于artifactId，可以理解为artifactName
                name = 'fineely-log'
                description = 'The rest interface log collection based on spring aop implementation supports kakfa and openFeign'
                url = 'https://github.com/Big-billed-shark/fineely-log'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://mit-license.org/'
                    }
                }
                developers {
                    developer {
                        name = 'Kepler Lei'
                        email = 'kepler_16b@163.com'
                    }
                }
                scm {
                    url = 'https://github.com/Big-billed-shark/fineely-log'
                    connection = 'scm:git:https://github.com/Big-billed-shark/fineely-log.git'
                    developerConnection = 'scm:git:https://github.com/Big-billed-shark/fineely-log.git'
                }
            }
        }
    }
    repositories {
        maven {
            name 'fineely-log'
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}


signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options.encoding = "UTF-8"
    failOnError = false
}

jar {
    baseName = "$archivesBaseName"
    version = "$project.version"
    enabled = true
    classifier = ''
}